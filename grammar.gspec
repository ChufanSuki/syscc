Ascending order of priority:
== !=
< <= > >=
+ -
* /
unary+ unary-
( )

program     = stmt*
stmt        = "return" expr ";" | expr-stmt | "{" compound-stmt | "if" "(" expr ")" stmt ("else" stmt)? | "for" "(" expr-stmt expr? ";" expr? ")" stmt | "while" "(" expr ")" stmt 
expr-stmt   = expr? ";"
compound-stmt = stmt* "}"
expr        = assign
assign      = equality ("=" assign)?
equality    = relational ("==" relational | "!=" relational)*
relational  = add ("<" add | "<=" add | ">" add | ">=" add)*
add         = mul ("+" mul | "-" mul)*
mul         = unary ("*" unary | "/" unary)*
unary       = ("+" | "-" | "*" | "&")? primary
primary     = num | "(" expr ")" | ident
